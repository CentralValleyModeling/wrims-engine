// This is the root build.gradle, which defines a few project-wide properties.
// Notably, this project sets the version of all projects, as well as the group name.

plugins {
    id "com.palantir.git-version" version "4.2.0"
    id 'base'
    id 'org.sonarqube' version '6.3.1.5724'
}

// This utilizes Git Tag based versioning to set the version of the project based on the current Git Branch or Tag.
// If a tag is checked out, the version will be the name of the most recent tag on this commit.
// Otherwise, the version will be last version suffixed with a ".9999" to indicate that it is a snapshot.
def versionLabel(gitInfo) {
    def tag = gitInfo.lastTag
    return gitInfo.branchName == null ? tag : tag + ".9999"
}

sonar {
    properties {
        property "sonar.projectKey", "CentralValleyModeling_wrims-engine"
        property "sonar.organization", "dwr-cvm"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.pullrequest.provider", "GitHub"
        property "sonar.pullrequest.github.repository", "CentralValleyModeling/wrims-engine"
        property "sonar.coverage.jacoco.xmlReportPaths", "$rootDir/wrims-core/build/reports/jacoco/test/jacocoTestReport.xml"
    }
}

tasks.register('copyDependencies', Copy) {
    into 'build/tmp/libs'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

allprojects {
    group = 'gov.ca.water'
    version = versionLabel(versionDetails())
}