// This is the root build.gradle, which defines a few project-wide properties.
// Notably, this project sets the version of all projects, as well as the group name.

plugins {
    id "com.palantir.git-version" version "3.0.0"
    id 'base'
}

// This utilizes Git Tag based versioning to set the version of the project based on the current Git Branch or Tag.
// If a tag is checked out, the version will be the name of the most recent tag on this commit.
// Otherwise, the version will be last version suffixed with a ".9999" to indicate that it is a snapshot.
def versionLabel(gitInfo) {
    def tag = gitInfo.lastTag
    return gitInfo.branchName == null ? tag : tag + ".9999"
}

tasks.register('copyDependencies', Copy) {
    into 'build/tmp/libs'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

allprojects {
    group = 'gov.ca.water'
    version = versionLabel(versionDetails())

    // JDK 21+ resolution for Lookup library bug: Issue #194
    configurations.configureEach {
        resolutionStrategy.dependencySubstitution {substitution ->
            substitution.substitute(module("mil.army.usace.hec:lookup")).using(module(libs.lookup.compat.get().toString()))
                    .because("WRIMS uses upstream NetBeans Lookup for Java 21 Compatibility.")
        }
    }
}