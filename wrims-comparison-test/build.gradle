plugins{
    id 'library.deps-conventions'
    id 'library.java-conventions'
    id 'maven-publish'
    id 'distribution'
}
configurations{
    windows_x64
    testProjects
    comparisonReport
}

dependencies {
    implementation(project(":wrims-core")){
        exclude group: "org.testng"
    }

    windows_x64(libs.javaHeclib){artifact {type = 'zip'}}
    windows_x64 libs.run.libs
    testProjects libs.cal.lite.test
    comparisonReport libs.system.summary.report.tool

    // Ensure the report tool is available on the test JVM classpath for utility launchers
    testRuntimeOnly libs.system.summary.report.tool

    // Cucumber JVM for Java with JUnit Platform engine
    testImplementation libs.bundles.cucumber


    // Azure Storage Blob for downloading project files from public container
    testImplementation(libs.azure.storage.blob)
}

// Load environment variables for the test task from a local file if present
// This file is intentionally git-ignored: see .gitignore rule /wrims-comparison-test/.env.*
def envFile = file("$projectDir/.env.cucumber")

tasks.register('getNatives', Sync) { syncTask ->
    syncTask.from configurations.windows_x64.collect { zipTree(it) }
    syncTask.into file("$buildDir/lib")
}

tasks.withType(Test).configureEach {
    // Ensure native libraries (e.g., jCbc.dll) are available under build/lib before tests run
    dependsOn("getNatives")

    if (envFile.exists()) {
        logger.lifecycle("Loading test environment from ${envFile}")
        envFile.readLines()
            .findAll { it && !it.startsWith('#') }
            .each { line ->
                def idx = line.indexOf('=')
                if (idx > 0) {
                    def k = line.substring(0, idx).trim()
                    def v = line.substring(idx + 1).trim().replaceAll('^[\"\']|[\"\']$', '')
                    environment k, v
                }
            }
    }
}

tasks.register("testExecute", JavaExec) {
    group = "compute-tests"
    def compareDirName = "$buildDir/testProjects/CalLite4.1_TF"
    dependsOn(tasks.named("getNatives"))
    dependsOn(tasks.named("getTestProjects"))
    workingDir "${buildDir}/testProjects/"
    classpath = files("${compareDirName}/Run/external")
    classpath += sourceSets.main.runtimeClasspath
    mainClass = "gov.ca.water.wrims.engine.core.components.ControllerBatch"
    args "-config=$buildDir/${project.findProperty('configFilePath') ?: "testProjects/CalLite4.1_TF/Test_02.config"}"
    environment 'PATH', "${compareDirName}/Run/external;$buildDir/lib;${System.getenv('PATH')}"
    systemProperty "java.library.path", "${compareDirName}/Run/external;$buildDir/lib"

    jvmArgs "-Xmx4096m", "-Xss1024K", "-XX:+CreateMinidumpOnCrash"

    doFirst {
        def wd = file(workingDir as String).absolutePath
        def cpFiles = classpath?.files ?: []
        def cpPath = classpath?.asPath
        def jvm = (jvmArgs ?: []).join(' ')
        def sysProps = (systemProperties ?: [:]).collect { k, v -> "-D${k}=${v}" }.join(' ')
        def mainCls = (mainClass instanceof Provider) ? mainClass.get() : mainClass
        def argsLine = (args ?: []).collect { it.toString().contains(' ') ? '"' + it + '"' : it }.join(' ')
        def javaExe = new File(System.getProperty('java.home'), "bin${File.separator}java").absolutePath
        def envPath = (environment?.get('PATH') ?: System.getenv('PATH'))

        logger.lifecycle("--- testExecute JavaExec details ---")
        logger.lifecycle("workingDir: ${wd}")
        logger.lifecycle("mainClass: ${mainCls}")
        logger.lifecycle("classpath entries (${cpFiles.size()}):")
        cpFiles.each { f -> logger.lifecycle("  - ${f}") }
        logger.lifecycle("java.class.path (asPath): ${cpPath}")
        logger.lifecycle("jvmArgs: ${jvm}")
        logger.lifecycle("systemProperties: ${systemProperties}")
        logger.lifecycle("JAVA_HOME: ${System.getProperty('java.home')}")
        logger.lifecycle("java.library.path: ${systemProperties?.get('java.library.path')}")
        logger.lifecycle("PATH: ${envPath}")

        def fullCmd = "\"${javaExe}\" ${jvm} ${sysProps} -cp \"${cpPath}\" ${mainCls} ${argsLine}".trim()
        logger.lifecycle("Full JavaExec command:")
        logger.lifecycle(fullCmd)
        logger.lifecycle("--- end JavaExec details ---")
    }
}


tasks.register("executeDcr2023", JavaExec) {
    group = "compute-tests"
    def configFile = "test.config"
    def projectFolder = "/testProjects/dcr2023"
    def compareDirName = "$buildDir${projectFolder}"
    dependsOn(tasks.named("getNatives"))
    workingDir "${buildDir}/testProjects/"
    classpath = files("${compareDirName}/Run/external")
    classpath += sourceSets.main.runtimeClasspath
    mainClass = "gov.ca.water.wrims.engine.core.components.ControllerBatch"
    args "-config=$buildDir/${project.findProperty('configFilePath') ?: "${projectFolder}/${configFile}"}"
    environment 'PATH', "${compareDirName}/Run/external;$buildDir/lib;${System.getenv('PATH')}"
    systemProperty "java.library.path", "${compareDirName}/Run/external;$buildDir/lib"

    jvmArgs "-Xmx4096m", "-Xss1024K", "-XX:+CreateMinidumpOnCrash"
}

tasks.register("testReport", JavaExec) {
    dependsOn(tasks.named("testExecute"))
    workingDir "${buildDir}/testProjects/"
    classpath = configurations.comparisonReport
    mainClass = "gov.ca.dwr.callite.Batch"
    def inputFile = file("$buildDir/${project.findProperty('inputFilePath') ?: "testProjects/callite_version_check_dss6_6.inp"}")
    args inputFile
    inputs.file(inputFile)

    systemProperty "java.library.path", "$buildDir/lib"
    environment 'PATH', "$buildDir/lib;${System.getenv('PATH')}"
    jvmArgs "-Xmx4096m", "-Xss1024K", "-XX:+CreateMinidumpOnCrash"

    def outputFile = file("$buildDir/testProjects/Callite_update_compare_6_6.pdf")
    outputs.file(outputFile)

    def outputErrorFile = file("$buildDir/testProjects/Callite_update_compare_6_6_VALIDATION_FAILURES.csv")
    doLast {
        if (execResult.exitValue == 2 && outputErrorFile.exists()) {
            outputs.file(outputErrorFile)
        }
    }
}

tasks.register('getTestProjects', Sync) { syncTask ->
    syncTask.from configurations.testProjects.collect { zipTree(it) }
    syncTask.into file("$buildDir/testProjects")
}

publishing {
    publications {
        pdf(MavenPublication) {
            artifact("$buildDir/testProjects/Callite_update_compare_6_6.pdf") {
                builtBy testReport
                extension 'pdf'
            }
        }
    }
}

// Runs only the compare_calList_41_TF.feature using a dedicated JUnit Platform suite test
tasks.register("runAzureCucumberTests", Test) {
    group = "verification"
    description = "Runs the compare_calList_41_TF.feature via JUnit suite"

    // Ensure native DLLs are prepared
    dependsOn("getNatives")
    // Ensure wrims-core artifacts exist when this test task runs
    dependsOn(":wrims-core:assemble")

    // Use JUnit Platform
    useJUnitPlatform()

    // Execute only the suite test class
    filter {
        includeTestsMatching "gov.ca.water.wrims.comparison.RunAzureCucumberTests"
    }

    // Show test output
    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed"
    }
}

// Make the default test task skip Azure
tasks.named('test', Test) {
    useJUnitPlatform()
    filter {
        // Do not fail the build when filters exclude all tests (e.g., Azure suite excluded)
        setFailOnNoMatchingTests(false)
        excludeTestsMatching "gov.ca.water.wrims.comparison.RunAzureCucumberTests"
    }
}
