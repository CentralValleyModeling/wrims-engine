plugins {
    id 'antlr'
    id 'library.deps-conventions'
    id 'library.java-conventions'
    id 'library.publishing-conventions'
    id 'jacoco'
}

sourceSets {
    src {
        antlr {
            srcDir "src/main/antlr"
        }
        java {
            srcDir "src/main/java"
            // IMPORTANT:
            // Do NOT add "generated-src" manually.
            // The Gradle ANTLR plugin automatically adds:
            //   build/generated-src/antlr/main
            // to the main source set.
        }
    }
}

configurations {
    windows_x64
}

dependencies {
    // --- native bundles (zip) ---
    windows_x64(libs.javaHeclib) { artifact { type = 'zip' } }
    windows_x64 libs.run.libs

    // --- platform/bom ---
    api(platform(project(":wrims-engine-bom")))
    api libs.hec.monolith

    // --- ANTLR codegen tool dependency (generator) ---
    antlr libs.antlr

    // --- main deps ---
    implementation libs.guava
    implementation libs.commons.io
    implementation libs.xstream
    implementation libs.javatuples
    implementation libs.java.object.diff
    implementation libs.kryo
    implementation libs.jep
    implementation libs.libtensorflow
    implementation libs.gurobi

    // dependency on testng exists in main as well as in test
    implementation libs.testng

    // HDF5 local jar
    implementation libs.jarhdf5

    // local jars
    implementation libs.linearsolver
    implementation libs.jcbc
    implementation libs.lpsolve55j
    implementation libs.xaoptimizer
    implementation libs.calsurrogate

    // GraalPy
    implementation libs.graalvm.polyglot
    implementation libs.graalvm.python
    implementation libs.graalvm.embedding

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.12'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.23.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.23.1'
    runtimeOnly 'com.lmax:disruptor:4.0.0'

}

// This moves .tokens files into the same generated-src files with the .java files
tasks.withType(AntlrTask).configureEach {
    if (name.startsWith('generate')) {
        doLast {
            logger.info("Copying .tokens files to corresponding java folders")

            copy {
                from "${buildDir}/generated-src/antlr/main"
                include '*Evaluator*.tokens'
                into "${buildDir}/generated-src/antlr/main/gov/ca/water/wrims/engine/core/evaluator/"
            }

            copy {
                from "${buildDir}/generated-src/antlr/main"
                include 'WreslTree*.tokens'
                into "${buildDir}/generated-src/antlr/main/gov/ca/water/wrims/engine/core/wreslparser/grammar/"
            }

            copy {
                from "${buildDir}/generated-src/antlr/main"
                include 'IncFileFinder.tokens'
                include 'WreslPlus.tokens'
                into "${buildDir}/generated-src/antlr/main/gov/ca/water/wrims/engine/core/wreslplus/grammar/"
            }

            delete "${buildDir}/generated-src/antlr/main/*.tokens"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

tasks.register("createBuildPropertiesFile", WriteProperties) { t ->
    outputFile(layout.buildDirectory.dir("resources/main").map {
        it.file("gov/ca/water/wrims/engine/build.properties")
    })
    comment = "this file is automatically created at build time, edits will be overwritten"
    property("version", project.version)
    property("gradle.version", gradle.gradleVersion)
    property("build.time", new Date().toString())
}

tasks.register('getNatives', Sync) { syncTask ->
    syncTask.from configurations.windows_x64.collect { zipTree(it) }
    syncTask.into file("${buildDir}/tmp/x64")
}

tasks.named('sourcesJar') {
    dependsOn tasks.named('generateGrammarSource')
}

tasks.named('build') {
    dependsOn tasks.named('copyDependencies')
}

tasks.named('processResources') {
    dependsOn tasks.named('createBuildPropertiesFile')
}

jacoco {
    toolVersion = "0.8.14"
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }
}
